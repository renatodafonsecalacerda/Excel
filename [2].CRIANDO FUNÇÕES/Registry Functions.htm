
<!-- saved from url=(0042)http://www.cpearson.com/excel/registry.htm -->
<html><head><meta http-equiv="Content-Type" content="text/html; charset=windows-1252">

<meta http-equiv="Content-Language" content="en-us">
<meta name="GENERATOR" content="Microsoft FrontPage 6.0">
<meta name="Keywords" content="Excel, Microsoft Excel, VBA, Excel97, Excel2000, Excel Formulas, Excel Macros, Excel Functions">
<meta name="Description" content="From Pearson Software Consulting, your complete resource for Excel solutions.">
<meta name="ProgId" content="FrontPage.Editor.Document">
<title>Registry Functions</title>
<link rel="stylesheet" type="text/css" href="./Registry Functions_files/Global.css">
<meta name="Microsoft Border" content="none, default">
</head>

<body bgcolor="#FFFFFF" text="#800000" vlink="#003366" leftmargin="10" link="#FF0000" style="">
<p style="background-color: #800000; color: #FFFF99; padding-left: 10; padding-top: 2; padding-bottom: 2"><font size="6" face="Arial">&nbsp;&nbsp;&nbsp;
Functions For Working With The Registry</font></p>
<table border="0" width="90%" cellspacing="1">
  <tbody><tr>
    <td width="5%"></td>
    <td width="93%"></td>
    <td width="10%"></td>
  </tr>
  <tr>
    <td width="5%" height="22">&nbsp;</td>
    <td width="93%" height="22">
    <font face="Arial">If you need to store information from one Excel session 
	to the next, such as user preferences or application configuration data, you 
	can store that data in the System Registry.&nbsp; This page describes about 
	10 VBA functions the are used to 
	read and write keys and values to the system Registry.&nbsp; There are about 
	an addition 10 functions that support the primary registry-related 
	functions. For information about manually working with the Registry using 
	the RegEdit program, see the <a href="http://www.cpearson.com/excel/ModifyRegistry.htm">Modify The System 
	Registry page</a>.</font>
	
	<p class="main">See <a href="http://www.cpearson.com/excel/RegistryWorx.aspx">this page</a> for a DLL component for working with the registry with VBA-friendly functions.</p>
	
	<p><font face="Arial">The procedures 
	presented here should give you full control over the Registry. It is assumed 
	that you are familiar with the system 
	Registry. Note that there is no "undo" functionality when dealing with the 
	System Registry and that if you delete or change a key or value, you may 
	cause serious problems with Windows, up to and including not being able to 
	start your system.&nbsp; It is up to you to ensure that you are not deleting 
	or changing critical system-related keys.</font></p>
	<p><font face="Arial">While the procedures described on this page will read, 
	write, create and delete any registry key, you should use only your own 
	registry keys. You can create keys for your own application, storing them in 
	the HKEY_CURRENT_USER section, with a key named something similar to&nbsp; 
	"Software\Pearson\ImportMultiModules". </font></p>
	<p><font face="Arial">Overall, the Registry is organized much like the 
	Windows file system. It is a hierarchical system, where keys may contain 
	keys that contain values that have a specific value. The word "value" is 
	used in two separate but related contexts. A key may contain one or more 
	named "values", each of which contains either a String or Long value.&nbsp; 
	For example, the key "HKEY_CURRENT_USER\Software\Microsoft\Office\12.0\Excel\Options" 
	contains many named values, each of which has a string or numeric value, 
	such as the "AltStartup" value which has value equal to the folder you 
	specified as an alternative startup directory for Excel. In the procedures 
	described on this page and available in the downloadable files, the term "ValueName" 
	is used to specify the named value (e.g, "AltStartup") and the term "ValueValue" 
	is used to specify the contents of ValueName. For example, "ValueName" might 
	refer to "AltStartup" and "ValueValue" refers to the contents of "AltStartup", 
	such as "C:\XLStart". </font></p>
	<p><font face="Arial">The system registry is divide into parts or sections 
	call <i>hives</i>. A <i>hive</i> is a distinct set of keys and their values. 
	For example, the key "Software\Pearson\ImportMultiModules" and all of the 
	values within this key comprise a hive. As long as you manipulate keys and 
	value only within your own hive, you shouldn't encounter any problems.
	</font></p>
	<p><font face="Arial">You can examine the contents of the Registry and add, 
	change, or delete keys and values using RegEdit program. On the Windows 
	Start menu, choose Run, and enter </font>
	<font color="#003366" face="Courier New">RegEdit</font><font face="Arial">. 
	This will start the Registry Editor program. Remember that all edits to the 
	registry are done "live".&nbsp; Once you change or delete a key or value, 
	there is no way to undo that action or exit RegEdit without a save. Before 
	you modify the system registry, be <i>sure</i> you are working with the 
	proper key in the proper hive.</font></p>
	<p><font face="Arial">Note that there is nothing specific to 
	Excel in the code. <span class="goog_qs-tidbit goog_qs-tidbit-0">This code can be used in any application that supports 
	VBA or in VB6.</span> </font>
	</p><p><font face="Arial">These 
	functions support reading and writing values of either String or Long data 
	types. If you attempt to store another numeric data type (e.g., Double), it 
	will be converted to a Long, and thus there is the possibility of data loss 
	(the fractional portion of the number will be lost and the integer portion 
	may be rounded). If you need to store a Single or Double value, convert it 
	to a string using the CStr function and store it as a String data type. 
	Incompatible types like objects, arrays, and user-defined types will cause 
	an error to occur.</font></p><p><font face="Arial">You can
	<a href="http://www.cpearson.com/Zips/modRegistry.zip">download a bas module here</a> or a
	<a href="http://www.cpearson.com/Zips/Registry.zip">complete workbook here</a>. These functions 
	require the <a href="http://www.cpearson.com/Zips/modFormatMessage.zip">
	modGetSystemErrorMessageText</a> module that retrieves text descriptions of 
	system error numbers. You can <a href="http://www.cpearson.com/excel/_vti_cnf/FormatMessage.htm">read 
	about this module here</a> or <a href="http://www.cpearson.com/Zips/modFormatMessage.zip">
	download it here</a>. You can download the <a href="http://www.cpearson.com/Zips/modRegistry.zip">
	modRegistry bas module file here</a> or a <a href="http://www.cpearson.com/Zips/Registry.zip">
	complete workbook here</a>. The procedures in the module and described on 
	this page call upon one another. You are strongly urged to import the entire 
	module into your project rather than copy/pasting individual procedures.</font></p><p>
	<font face="Arial"><b>Error Reporting</b><br>Errors that may arise in the execution of the procedures are reported in 4 
	Public variables. </font>
	</p><blockquote>
		<p><font face="Arial"><b>Public G_Reg_AppErrNum As Long</b><br>This variable contains the error number assigned by the module procedures. 
	This will be one of the public constants defined in the module beginning 
	with C_REG_ERR_.<br><br><b>Public G_Reg_AppErrText As String</b><br>This is a text description of G_Reg_AppErrNum
		<br>&nbsp;<br><b>Public G_Reg_SysErrNum As Long</b><br>This is the system error number, the error number returned by the Registry 
	API functions.<br><br><b>Public G_Reg_SysErrText As String</b><br>This is a text description of G_Reg_SysErrNum.
		<br>&nbsp;</font></p></blockquote>
	<p><font face="Arial">If a function returns False (or Null) 
	indicating that the operation was unsuccessful, you should examine the 
	variable listed above to determine the cause of the error.</font></p><p>
	<font face="Arial">In all functions, BaseKey is one of the following:</font></p><pre><font color="#003366">    HKEY_CURRENT_USER
    HKEY_LOCAL_MACHINE
    HKEY_CLASSES_ROOT
    HKEY_CURRENT_CONFIG
    HKEY_DYN_DATA
    HKEY_PERFORMANCE_DATA
    HKEY_USERS</font></pre>
	<p><font face="Arial">It is <i>strongly</i> recommended that you modify <i>
	only</i> keys in HKEY_CURRENT_USER. </font>
	</p><p><font face="Arial"><br>
	The functions provided in the module are as follows. </font>
	</p><p><b><font face="Arial">RegistryCreateKey</font></b></p><pre>    <font color="#003366">Public Function RegistryCreateKey(BaseKey As Long, KeyName As String) As Boolean</font></pre>
	<p><font face="Arial">This function&nbsp; create a new key named KeyName in 
	the BaseKey section of the registry. The function returns True or False 
	indicating success. If the key already exists, the result is True.</font></p><p>
	<b><font face="Arial">RegistryCreateValue</font></b></p><pre><font color="#003366">    Public Function RegistryCreateValue(BaseKey As Long, KeyName As String, _
        ValueName As String, ValueValue As Variant, _
        Optional CreateKeyIfNotExists As Boolean = False) As Boolean</font></pre>
	<p><font face="Arial">This function creates a new value in the registry 
	named ValueName in KeyName in BaseKey. If CreateKeyIfNotExists is True, the 
	key named in KeyName is created if it does not exist. If the value named in 
	ValueName already exists, its value is updated to the new value in 
	ValueValue. This function returns True or False indicating success. 
	ValueValue must be a String or Long type value.</font></p><p><b>
	<font face="Arial">RegistryDeleteKey</font></b></p><pre>    <font color="#003366">Public Function RegistryDeleteKey(BaseKey As Long, KeyName As String) As Boolean</font></pre>
	<p><font face="Arial">This procedure deletes KeyName and all subkeys and 
	values within KeyName. It returns True or False indicating success. If 
	KeyName does not exist, the result is True.</font></p><p><b><font face="Arial">
	RegistryDeleteValue</font></b></p><pre>    <font color="#003366">Public Function RegistryDeleteValue(BaseKey As Long, KeyName As String, ValueName As String) As Boolean</font></pre>
	<p><font face="Arial">This procedure deletes the value named by ValueName 
	from KeyName. The function returns True or False indicating success. If the 
	value named by ValueName does not exist, the result is True.</font></p><p><b>
	<font face="Arial">RegistryGetValue</font></b></p><pre>    <font color="#003366">Public Function RegistryGetValue(BaseKey As Long, KeyName As String, _
        ValueName As String) As Variant</font></pre>
	<p><font face="Arial">This function returns the value of the value named in 
	ValueName of KeyName. It returns NULL if an error occurs. This function, 
	along with the RegistryUpdateValue function, are the primary workers of 
	these procedures. Most of what you need to do can be accomplished with these 
	two functions.</font></p><p><b><font face="Arial">RegistsryGetValueType</font></b></p><pre>    <font color="#003366">Public Function RegistryGetValueType(BaseKey As Long, KeyName As String, ValueName As String) As REG_DATA_TYPE</font></pre>
	<p><font face="Arial">This function returns the data type of the Value 
	stored in ValueName of KeyName. It will return either&nbsp; REG_INVALID = -1 
	(invalid type), REG_SZ = 1 (String type) or&nbsp; REG_DWORD = 4 (Long type).</font></p><p>
	<b><font face="Arial">RegistryKeyExists</font></b></p><pre><font color="#003366">    Public Function RegistryKeyExists(BaseKey As Long, KeyName As String,  _ 
        Optional CreateIfNotExists As Boolean = False) As Boolean</font></pre>
	<p><font face="Arial">This function returns True or False indicating whether 
	the key named in KeyName exists. If the CreateIfNotExists parameter is True, the key will 
	be created and the result will be True if the key was successfully created. </font></p><p><b><font face="Arial">
	RegistryUpdateValue</font></b></p><pre><font color="#003366">    Public Function RegistryUpdateValue(BaseKey As Long, KeyName As String, _
        ValueName As String, NewValue As Variant, Optional CreateIfNotExists As Boolean = True) As Boolean</font></pre>
	<p><font face="Arial">This procedure updates the value of the existing value 
	named by ValueName in KeyName with the new value NewValue. If value named in 
	ValueName does not exist and CreateIfNotExists is True, the value is&nbsp; 
	create. If CreateIfNotExists is True and the key named in KeyName does not 
	exist, then the key is create. Therefore, you can use this function to 
	create new values in new keys, and the procedure will automatically create 
	the new keys and values as required. This function returns True or False 
	indicating success.</font></p><p><b><font face="Arial">RegistryValueExists</font></b></p><pre><font color="#003366">    Public Function RegistryValueExists(BaseKey As Long, KeyName As String, _
        ValueName As String, Optional CreateIfNotExists As Boolean = False, _
        Optional CreateType As REG_DATA_TYPE = REG_DWORD) As Boolean</font></pre>
	<p><font face="Arial">This function returns True or False indicating whether 
	a registry value named ValueName exists in the key KeyName in BaseKey. If 
	CreateIfNotExists is True, the key and/or the value is create if it does not 
	exist. The CreateType parameter indicates whether to create a String type 
	value (CreateType := REG_SZ = 1) or a Long type value (CreateType := REG_DWORD 
	= 4).</font></p><p>
	<font face="Arial">The complete VBA code follows (not including the 
	GetSystemErrorMessageText function, <a href="http://www.cpearson.com/excel/FormatMessage.htm">available 
	here</a>)</font></p><pre><font color="#003366">Option Explicit
Option Compare Text
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' modRegistry
' By Chip Pearson, www.cpearson.com, chip@cpearson.com
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
' This function provides several functions related to working with keys and values in the system
' registry. These routines call upon one another, so you should import this entire module into
' your project rather than just copy/pasting an individual procedures.
'
' This module is described and avaialable for download at http://www.cpearson.com/Excel/Registry.htm.
'
' Error conditions and details are reported in the following public variables:
'       G_Reg_AppErrNum As Long         Returns the module-defined error number.
'       G_Reg_AppErrText As String      Returns the text description of G_Reg_AppErrNum
'       G_Reg_SysErrNum As Long         Returns the system error number, usually the value of Err.LastDllError
'       G_Reg_SysErrText As String      Returns the text description associated with G_Reg_SysErrNum, the text
'                                       returned from GetSystemErrorMessageText.
'
' This module requires the moGetSystemErrorMessageText module, described and available for download at
' http://www.cpearson.com/excel/FormatMessage.htm. This module itself is described and available for
' download at http://www.cpearson.com/excel/registry.htm.
'
' In all functions with a BaseKey parameter, the value of BaseKey must be either HKEY_CURRENT_USER (or HKCU) or
' HKEY_LOCAL_MACHINE (or HKML). Any other value is invalid.
'
' Public Functions In This Module:
' --------------------------------
'   RegistryGetValue
'   RegistryGetValueType
'   RegistryCreateKey
'   RegistryCreateValue
'   RegistryDeleteKey
'   RegistryDeleteValue
'   RegistryKeyExists
'   RegistryValueExists
'   RegistryUpdateValue
'
' See http://www.cpearson.com/excel/registry.htm for details about these procedures.
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Error Constants
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Public Const C_REG_ERR_NO_ERROR = 0
Public Const C_REG_ERR_INVALID_BASE_KEY = vbObjectError + 1
Public Const C_REG_ERR_INVALID_DATA_TYPE = vbObjectError + 2
Public Const C_REG_ERR_KEY_NOT_FOUND = vbObjectError + 3
Public Const C_REG_ERR_VALUE_NOT_FOUND = vbObjectError + 4
Public Const C_REG_ERR_DATA_TYPE_MISMATCH = vbObjectError + 5
Public Const C_REG_ERR_ENTRY_LOCKED = vbObjectError + 6
Public Const C_REG_ERR_INVALID_KEYNAME = vbObjectError + 7
Public Const C_REG_ERR_UNABLE_TO_OPEN_KEY = vbObjectError + 8
Public Const C_REG_ERR_UNABLE_TO_READ_KEY = vbObjectError + 9
Public Const C_REG_ERR_UNABLE_TO_CREATE_KEY = vbObjectError + 10
Public Const C_REG_ERR_UBABLE_TO_READ_VALUE = vbObjectError + 11
Public Const C_REG_ERR_UNABLE_TO_UDPATE_VALUE = vbObjectError + 12
Public Const C_REG_ERR_UNABLE_TO_CREATE_VALUE = vbObjectError + 13
Public Const C_REG_ERR_UNABLE_TO_DELETE_KEY = vbObjectError + 14
Public Const C_REG_ERR_UNABLE_TO_DELETE_VALUE = vbObjectError + 15




'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' API Constants
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Public Const HKEY_CURRENT_USER As Long = &amp;H80000001
Public Const HKEY_LOCAL_MACHINE As Long = &amp;H80000002
Public Const HKEY_CLASSES_ROOT = &amp;H80000000
Public Const HKEY_CURRENT_CONFIG = &amp;H80000005
Public Const HKEY_DYN_DATA = &amp;H80000006
Public Const HKEY_PERFORMANCE_DATA = &amp;H80000004
Public Const HKEY_USERS = &amp;H80000003


Public Const HKCU = HKEY_CURRENT_USER
Public Const HKLM = HKEY_LOCAL_MACHINE


Private Const REGSTR_MAX_VALUE_LENGTH  As Long = &amp;H100

Private Const KEY_QUERY_VALUE = &amp;H1
Private Const KEY_SET_VALUE = &amp;H2
Private Const KEY_CREATE_SUB_KEY = &amp;H4
Private Const KEY_ENUMERATE_SUB_KEYS = &amp;H8
Private Const KEY_NOTIFY = &amp;H10
Private Const KEY_CREATE_LINK = &amp;H20
Private Const KEY_ALL_ACCESS = &amp;H3F

Private Const REG_CREATED_NEW_KEY = &amp;H1
Private Const REG_OPENED_EXISTING_KEY = &amp;H2

Private Const STANDARD_RIGHTS_ALL = &amp;H1F0000
Private Const SPECIFIC_RIGHTS_ALL = &amp;HFFFF

Private Const REG_OPTION_NON_VOLATILE = 0&amp;
Private Const REG_OPTION_VOLATILE = &amp;H1

Private Const ERROR_SUCCESS = 0&amp;
Private Const ERROR_ACCESS_DENIED = 5
Private Const ERROR_INVALID_DATA = 13&amp;
Private Const ERROR_MORE_DATA = 234 '  dderror
Private Const ERROR_NO_MORE_ITEMS = 259

Private Const S_OK = &amp;H0
Private Const MAX_DATA_BUFFER_SIZE = 1024


'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' API Types
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Private Type SECURITY_ATTRIBUTES
  nLength As Long
  lpSecurityDescriptor As Long
  bInheritHandle As Boolean
End Type

Private Type FILETIME
  dwLowDateTime As Long
  dwHighDateTime As Long
End Type

Public Enum REG_DATA_TYPE
    REG_INVALID = -1 ' Invalid
    REG_SZ = 1       ' String
    REG_DWORD = 4    ' Long
End Enum

Private Type ACL
        AclRevision As Byte
        Sbz1 As Byte
        AclSize As Integer
        AceCount As Integer
        Sbz2 As Integer
End Type

Private Type SECURITY_DESCRIPTOR
        Revision As Byte
        Sbz1 As Byte
        Control As Long
        Owner As Long
        Group As Long
        Sacl As ACL
        Dacl As ACL
End Type


'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' API Declares
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Private Declare Function RegCloseKey Lib "advapi32.dll" ( _
    ByVal HKey As Long) As Long

Private Declare Function RegCreateKeyEx Lib "advapi32.dll" Alias "RegCreateKeyExA" ( _
    ByVal HKey As Long, _
    ByVal lpSubKey As String, _
    ByVal Reserved As Long, _
    ByVal lpClass As String, _
    ByVal dwOptions As Long, _
    ByVal samDesired As Long, _
    lpSecurityAttributes As SECURITY_ATTRIBUTES, _
    phkResult As Long, _
    lpdwDisposition As Long) As Long

Private Declare Function RegDeleteKey Lib "advapi32.dll" Alias "RegDeleteKeyA" ( _
    ByVal HKey As Long, _
    ByVal lpSubKey As String) As Long

Private Declare Function RegOpenKey Lib "advapi32.dll" Alias "RegOpenKeyA" ( _
    ByVal HKey As Long, _
    ByVal lpSubKey As String, _
    phkResult As Long) As Long

Private Declare Function RegDeleteValue Lib "advapi32.dll" Alias "RegDeleteValueA" ( _
    ByVal HKey As Long, _
    ByVal lpValueName As String) As Long

Private Declare Function RegEnumKey Lib "advapi32.dll" Alias "RegEnumKeyA" ( _
    ByVal HKey As Long, _
    ByVal dwIndex As Long, _
    ByVal lpName As String, _
    ByVal cbName As Long) As Long

Private Declare Function RegEnumKeyEx Lib "advapi32.dll" Alias "RegEnumKeyExA" ( _
    ByVal HKey As Long, _
    ByVal dwIndex As Long, _
    ByVal lpName As String, _
    lpcbName As Long, _
    ByVal lpReserved As Long, _
    ByVal lpClass As String, _
    lpcbClass As Long, _
    lpftLastWriteTime As FILETIME) As Long

Private Declare Function RegEnumValue Lib "advapi32.dll" Alias "RegEnumValueA" ( _
    ByVal HKey As Long, _
    ByVal dwIndex As Long, _
    ByVal lpValueName As String, _
    lpcbValueName As Long, _
    ByVal lpReserved As Long, _
    lpType As Long, _
    lpData As Byte, _
    lpcbData As Long) As Long

Private Declare Function RegFlushKey Lib "advapi32.dll" ( _
    ByVal HKey As Long) As Long

Private Declare Function RegGetKeySecurity Lib "advapi32.dll" ( _
    ByVal HKey As Long, _
    ByVal SecurityInformation As Long, _
    pSecurityDescriptor As SECURITY_DESCRIPTOR, _
    lpcbSecurityDescriptor As Long) As Long

Private Declare Function RegQueryInfoKey Lib "advapi32.dll" Alias "RegQueryInfoKeyA" ( _
    ByVal HKey As Long, _
    ByVal lpClass As String, _
    lpcbClass As Long, _
    ByVal lpReserved As Long, _
    lpcSubKeys As Long, _
    lpcbMaxSubKeyLen As Long, _
    lpcbMaxClassLen As Long, _
    lpcValues As Long, _
    lpcbMaxValueNameLen As Long, _
    lpcbMaxValueLen As Long, _
    lpcbSecurityDescriptor As Long, _
    lpftLastWriteTime As FILETIME) As Long

Private Declare Function RegQueryValue Lib "advapi32.dll" Alias "RegQueryValueA" ( _
    ByVal HKey As Long, _
    ByVal lpSubKey As String, _
    ByVal lpValue As String, _
    lpcbValue As Long) As Long

Private Declare Function RegQueryValueEx Lib "advapi32.dll" Alias "RegQueryValueExA" ( _
    ByVal HKey As Long, _
    ByVal lpValueName As String, _
    ByVal lpReserved As Long, _
    lpType As Long, _
    lpData As Any, _
    lpcbData As Long) As Long

Private Declare Function RegSetValueEx Lib "advapi32.dll" Alias "RegSetValueExA" ( _
    ByVal HKey As Long, _
    ByVal lpValueName As String, _
    ByVal Reserved As Long, _
    ByVal dwType As Long, _
    lpData As Any, _
    ByVal cbData As Long) As Long

Private Declare Function RegSetValueExStr Lib "advapi32" Alias "RegSetValueExA" ( _
    ByVal HKey As Long, _
    ByVal lpValueName As String, _
    ByVal Reserved As Long, _
    ByVal dwType As Long, _
    ByVal szData As String, _
    ByVal cbData As Long) As Long

Private Declare Function RegSetValueExLong Lib "advapi32" Alias "RegSetValueExA" ( _
    ByVal HKey As Long, _
    ByVal lpValueName As String, _
    ByVal Reserved As Long, _
    ByVal dwType As Long, _
    szData As Long, _
    ByVal cbData As Long) As Long

Private Declare Function RegOpenKeyEx Lib "advapi32" Alias "RegOpenKeyExA" ( _
    ByVal HKey As Long, _
    ByVal lpSubKey As String, _
    ByVal ulOptions As Long, _
    ByVal samDesired As Long, _
    phkResult As Long) As Long

Private Declare Function RegQueryValueExStr Lib "advapi32" Alias "RegQueryValueExA" ( _
    ByVal HKey As Long, _
    ByVal lpValueName As String, _
    ByVal lpReserved As Long, _
    ByRef lpType As Long, _
    ByVal szData As String, _
    ByRef lpcbData As Long) As Long


'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Application Constants
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Public Type RegValue
    ValueName As String
    ValueValue As Variant
End Type

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Public  Variables
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Public G_Reg_AppErrNum As Long
Public G_Reg_AppErrText As String
Public G_Reg_SysErrNum As Long
Public G_Reg_SysErrText As String



'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Private Variables
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''


'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Public Functions
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Public Function RegistryGetValue(BaseKey As Long, KeyName As String, _
    ValueName As String) As Variant
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' RegistryGetValue
' This funciton gets the value of of the specified ValueName in the
' key KeyName in the base key BaseKey. Returns NULL if an error
' occurred.
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Dim HKey As Long
Dim Res As Long
Dim RegDataType As REG_DATA_TYPE
Dim LenData As Long
Dim LongData As Long
Dim StringData As String
Dim IntArr(0 To 1024) As Integer
Dim LenStringData As Long

ResetErrorVariables

If IsValidBaseKey(BaseKey:=BaseKey) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryGetValue = Null
    Exit Function
End If

If IsValidKeyName(KeyName:=KeyName) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryGetValue = Null
    Exit Function
End If

If RegistryKeyExists(BaseKey:=BaseKey, KeyName:=KeyName) = False Then
    G_Reg_AppErrNum = C_REG_ERR_KEY_NOT_FOUND
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryGetValue = Null
    Exit Function
End If

RegDataType = RegistryGetValueType(BaseKey:=BaseKey, KeyName:=KeyName, ValueName:=ValueName)
HKey = OpenRegistryKey(BaseKey:=BaseKey, KeyName:=KeyName)
If HKey = 0 Then
    G_Reg_SysErrNum = Res
    G_Reg_SysErrText = GetSystemErrorMessageText(ErrorNumber:=Res)
    G_Reg_AppErrNum = C_REG_ERR_UNABLE_TO_OPEN_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryGetValue = Null
    Exit Function
End If


If RegDataType = REG_DWORD Then
    '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    ' Data is Long data-type.
    '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    Res = RegQueryValueEx(HKey:=HKey, lpValueName:=ValueName, lpReserved:=0&amp;, _
        lpType:=RegDataType, lpData:=LongData, lpcbData:=Len(LongData))
    If Res = ERROR_SUCCESS Then
        RegistryGetValue = LongData
        Exit Function
    Else
        G_Reg_SysErrNum = Res
        G_Reg_SysErrText = GetSystemErrorMessageText(ErrorNumber:=Res)
        G_Reg_AppErrNum = C_REG_ERR_UBABLE_TO_READ_VALUE
        G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
        RegCloseKey HKey
        RegistryGetValue = Null
        Exit Function
    End If
ElseIf RegDataType = REG_SZ Then
    '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    ' Data is String data-type.
    '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    StringData = String$(MAX_DATA_BUFFER_SIZE, vbNullChar)
    LenStringData = Len(StringData)
    Res = RegQueryValueExStr(HKey:=HKey, lpValueName:=ValueName, lpReserved:=0&amp;, _
        lpType:=RegDataType, szData:=StringData, lpcbData:=LenStringData)
    If Res &lt;&gt; ERROR_SUCCESS Then
        G_Reg_SysErrNum = Res
        G_Reg_SysErrText = GetSystemErrorMessageText(ErrorNumber:=Res)
        G_Reg_AppErrNum = C_REG_ERR_UBABLE_TO_READ_VALUE
        G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
        RegCloseKey HKey
        RegistryGetValue = Null
        Exit Function
    End If
    StringData = TrimToNull(StringData)
    RegistryGetValue = StringData
Else
    G_Reg_AppErrNum = C_REG_ERR_INVALID_DATA_TYPE
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryGetValue = Null
End If

End Function

Public Function RegistryKeyExists(BaseKey As Long, KeyName As String, _
    Optional CreateIfNotExists As Boolean = False) As Boolean
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' RegistryKeyExists
' Returns True or False indicating whether KeyName exists in BaseKey.
' Returns False if an error occurred. See the global error values
' for more information. If CreateIfNotExists is True and the
' key does not exist, it will be created.
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Dim HKey As Long
Dim Res As Long

ResetErrorVariables
If IsValidBaseKey(BaseKey:=BaseKey) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryKeyExists = False
End If

If IsValidKeyName(KeyName:=KeyName) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryKeyExists = False
End If

Res = RegOpenKey(HKey:=BaseKey, lpSubKey:=KeyName, phkResult:=HKey)
If Res = ERROR_SUCCESS Then
    RegistryKeyExists = True
Else
    RegistryKeyExists = False
    If CreateIfNotExists = True Then
        Res = RegistryCreateKey(BaseKey:=BaseKey, KeyName:=KeyName)
        RegistryKeyExists = CBool(Res)
    End If
End If

RegCloseKey HKey:=HKey

End Function

Public Function RegistryValueExists(BaseKey As Long, KeyName As String, _
    ValueName As String, Optional CreateIfNotExists As Boolean = False, _
    Optional CreateType As REG_DATA_TYPE = REG_DWORD) As Boolean
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' RegistryValueExists
' This returns True or False indicating whether ValueName exists in
' KeyName in BaseKey.
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''


Dim HKey As Long
Dim Res As Long

ResetErrorVariables
If IsValidBaseKey(BaseKey:=BaseKey) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryValueExists = False
End If

If IsValidKeyName(KeyName:=KeyName) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryValueExists = False
End If

HKey = OpenRegistryKey(BaseKey:=BaseKey, KeyName:=KeyName)
If HKey = 0 Then
    G_Reg_AppErrNum = C_REG_ERR_UNABLE_TO_OPEN_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryValueExists = False
End If

Res = RegQueryValueEx(HKey:=HKey, lpValueName:=ValueName, lpReserved:=0&amp;, lpType:=0&amp;, lpData:=0&amp;, lpcbData:=0&amp;)
If (Res = ERROR_SUCCESS) Or (Res = ERROR_MORE_DATA) Then
    RegistryValueExists = True
Else
    If CreateIfNotExists = True Then
        If CreateType = REG_DWORD Then
            Res = RegistryCreateValue(BaseKey:=BaseKey, KeyName:=KeyName, ValueName:=ValueName, _
                ValueValue:=0&amp;, CreateKeyIfNotExists:=True)
        Else
            Res = RegistryCreateValue(BaseKey:=BaseKey, KeyName:=KeyName, ValueName:=ValueName, _
                ValueValue:=vbNullString, CreateKeyIfNotExists:=True)
        End If
        If CBool(Res) = True Then
            RegistryValueExists = True
        Else
            RegistryValueExists = False
        End If
    End If
End If

RegCloseKey HKey

End Function

Public Function RegistryGetValueType(BaseKey As Long, KeyName As String, ValueName As String) As REG_DATA_TYPE
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' RegistryGetValueType
' This returns the data type of value named in ValueName. The procedures in
' this module support only Longs and Strings, so the result will be REG_SZ
' for a string, REG_DWORD for a Long, or REG_INVALID for any other data type.
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Dim Res As Long
Dim HKey As Long
Dim DataType As REG_DATA_TYPE

ResetErrorVariables

If IsValidBaseKey(BaseKey:=BaseKey) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryGetValueType = False
End If

If IsValidKeyName(KeyName:=KeyName) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryGetValueType = False
End If

Res = RegOpenKey(HKey:=BaseKey, lpSubKey:=KeyName, phkResult:=HKey)
If Res &lt;&gt; ERROR_SUCCESS Then
    G_Reg_AppErrNum = C_REG_ERR_UNABLE_TO_OPEN_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryGetValueType = REG_INVALID
    Exit Function
End If
    
Res = RegQueryValueEx(HKey:=HKey, lpValueName:=ValueName, lpReserved:=0&amp;, lpType:=DataType, lpData:=0&amp;, lpcbData:=0&amp;)
If (Res &lt;&gt; ERROR_SUCCESS) And (Res &lt;&gt; ERROR_MORE_DATA) Then
    G_Reg_AppErrNum = C_REG_ERR_UBABLE_TO_READ_VALUE
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryGetValueType = REG_INVALID
    RegCloseKey HKey
    Exit Function
End If
    
Select Case DataType
    Case REG_SZ
        RegistryGetValueType = REG_SZ
    Case REG_DWORD
        RegistryGetValueType = REG_DWORD
    Case Else
        RegistryGetValueType = REG_INVALID
End Select

RegCloseKey HKey

End Function

Public Function RegistryCreateValue(BaseKey As Long, KeyName As String, _
    ValueName As String, ValueValue As Variant, _
    Optional CreateKeyIfNotExists As Boolean = False) As Boolean
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' RegistryCreateValue
' This creates a value named ValueName in KeyName in BaseKey with a value
' of ValueValue. If the key named by KeyName does not exist, and
' CreateKeyIfNotExist is True, the key will be created. If the value
' already exists, its value is set to the new value if they are
' compatible data types.
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Dim HKey As Long
Dim Res As Long
Dim DataType As REG_DATA_TYPE
Dim StringValue As String
Dim LongValue As Long

ResetErrorVariables

If IsValidBaseKey(BaseKey:=BaseKey) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryCreateValue = False
    Exit Function
End If

If IsValidKeyName(KeyName:=KeyName) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryCreateValue = False
    Exit Function
End If

If RegistryKeyExists(BaseKey:=BaseKey, KeyName:=KeyName, _
    CreateIfNotExists:=CreateKeyIfNotExists) = False Then
    G_Reg_AppErrNum = C_REG_ERR_KEY_NOT_FOUND
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryCreateValue = False
    Exit Function
End If
     
    
If IsCompatibleValueValue(Var:=ValueValue) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_DATA_TYPE
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryCreateValue = False
    Exit Function
End If

If RegistryKeyExists(BaseKey:=BaseKey, KeyName:=KeyName, CreateIfNotExists:=False) = False Then
    If CreateKeyIfNotExists = True Then
        If RegistryKeyExists(BaseKey:=BaseKey, KeyName:=KeyName, CreateIfNotExists:=True) = False Then
            G_Reg_AppErrNum = C_REG_ERR_UNABLE_TO_CREATE_KEY
            G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
            RegistryCreateValue = False
            Exit Function
        End If
    Else
        G_Reg_AppErrNum = C_REG_ERR_KEY_NOT_FOUND
        G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
        RegistryCreateValue = False
        Exit Function
    End If
End If

If RegistryValueExists(BaseKey:=BaseKey, KeyName:=KeyName, ValueName:=ValueName) = True Then
    DataType = RegistryGetValueType(BaseKey:=BaseKey, KeyName:=KeyName, ValueName:=ValueName)
    If DataType = REG_SZ Then
        If VarType(ValueValue) &lt;&gt; vbString Then
            G_Reg_AppErrNum = C_REG_ERR_DATA_TYPE_MISMATCH
            G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
            RegistryCreateValue = False
            Exit Function
        Else
            '''''''''''''''''''''''''''''
            ' ValueValue is a string. OK.
            '''''''''''''''''''''''''''''
        End If
    Else
        '''''''''''''''''''''''''
        ' ValueValue is numeric
        '''''''''''''''''''''''''
    End If
Else
    '''''''''''''''''''''''
    ' Value does not exist.
    ' Set the DataType.
    '''''''''''''''''''''''
    If VarType(ValueValue) = vbString Then
        DataType = REG_SZ
    Else
        DataType = REG_DWORD
    End If
End If

If DataType = REG_DWORD Then
    LongValue = CLng(ValueValue)
    HKey = OpenRegistryKey(BaseKey:=BaseKey, KeyName:=KeyName)
    If HKey = 0 Then
        G_Reg_SysErrNum = Err.LastDllError
        G_Reg_SysErrText = GetSystemErrorMessageText(ErrorNumber:=G_Reg_SysErrNum)
        G_Reg_AppErrNum = C_REG_ERR_UNABLE_TO_OPEN_KEY
        G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
        RegCloseKey HKey
        RegistryCreateValue = False
        Exit Function
    End If
        
    Res = RegSetValueExLong(HKey:=HKey, lpValueName:=ValueName, Reserved:=0&amp;, _
        dwType:=REG_DWORD, szData:=LongValue, cbData:=Len(LongValue))
    If Res &lt;&gt; ERROR_SUCCESS Then
        G_Reg_AppErrNum = C_REG_ERR_UNABLE_TO_UDPATE_VALUE
        G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
        RegCloseKey HKey
        RegistryCreateValue = False
        Exit Function
    End If
Else
    StringValue = CStr(ValueValue)
    HKey = OpenRegistryKey(BaseKey:=BaseKey, KeyName:=KeyName)
    If HKey = 0 Then
        G_Reg_SysErrNum = Err.LastDllError
        G_Reg_SysErrText = GetSystemErrorMessageText(ErrorNumber:=G_Reg_SysErrNum)
        G_Reg_AppErrNum = C_REG_ERR_UNABLE_TO_OPEN_KEY
        G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
        RegCloseKey HKey
        RegistryCreateValue = False
        Exit Function
    End If
    Res = RegSetValueExStr(HKey:=HKey, lpValueName:=ValueName, Reserved:=0&amp;, _
        dwType:=REG_SZ, szData:=StringValue, cbData:=Len(StringValue))
    If Res &lt;&gt; ERROR_SUCCESS Then
        G_Reg_AppErrNum = C_REG_ERR_UNABLE_TO_UDPATE_VALUE
        G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
        RegistryCreateValue = False
        RegCloseKey HKey
        Exit Function
    End If
End If

RegCloseKey HKey
RegistryCreateValue = True

End Function

Public Function RegistryCreateKey(BaseKey As Long, KeyName As String) As Boolean
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' RegistryCreateKey
' This function creates a Key named KeyName in BaseKey. Returns True if successful
' or False if an error occurred.
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Dim Res As Long
Dim HKey As Long
Dim DataType As REG_DATA_TYPE
Dim SecAttrib As SECURITY_ATTRIBUTES
Dim Disposition As Long
ResetErrorVariables

If IsValidBaseKey(BaseKey:=BaseKey) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryCreateKey = False
End If

If IsValidKeyName(KeyName:=KeyName) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryCreateKey = False
End If

If RegistryKeyExists(BaseKey:=BaseKey, KeyName:=KeyName) = True Then
    '''''''''''''''''''''''''''
    ' Key already exist. Return
    ' True as if we created it.
    '''''''''''''''''''''''''''
    RegistryCreateKey = True
    Exit Function
End If

Res = RegCreateKeyEx(HKey:=BaseKey, lpSubKey:=KeyName, Reserved:=0&amp;, lpClass:=vbNullString, _
                    dwOptions:=REG_OPTION_NON_VOLATILE, samDesired:=KEY_ALL_ACCESS, _
                    lpSecurityAttributes:=SecAttrib, phkResult:=HKey, lpdwDisposition:=Disposition)
If Res &lt;&gt; ERROR_SUCCESS Then
    G_Reg_SysErrNum = Res
    G_Reg_SysErrText = GetSystemErrorMessageText(ErrorNumber:=G_Reg_SysErrNum)
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryCreateKey = False
    Exit Function
End If

RegistryCreateKey = True

End Function

Public Function RegistryDeleteValue(BaseKey As Long, KeyName As String, ValueName As String) As Boolean
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' RegistryDeleteValue
' This deletes a value in KeyName in BaseKey.  Returns True or False indicating
' success. The function returns True if the Value does not exist.
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Dim Res As Long
Dim HKey As Long
Dim DataType As REG_DATA_TYPE
Dim SecAttrib As SECURITY_ATTRIBUTES
Dim Disposition As Long

ResetErrorVariables

If IsValidBaseKey(BaseKey:=BaseKey) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryDeleteValue = False
    Exit Function
End If

If IsValidKeyName(KeyName:=KeyName) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryDeleteValue = False
    Exit Function
End If

If RegistryKeyExists(BaseKey:=BaseKey, KeyName:=KeyName, CreateIfNotExists:=False) = False Then
    G_Reg_AppErrNum = C_REG_ERR_KEY_NOT_FOUND
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryDeleteValue = False
    Exit Function
End If

HKey = OpenRegistryKey(BaseKey:=BaseKey, KeyName:=KeyName)
If HKey = 0 Then
    RegistryDeleteValue = False
    Exit Function
End If
If RegistryValueExists(BaseKey:=BaseKey, KeyName:=KeyName, ValueName:=ValueName) = False Then
    RegCloseKey HKey
    RegistryDeleteValue = True
    Exit Function
End If

Res = RegDeleteValue(HKey:=HKey, lpValueName:=ValueName)
If Res &lt;&gt; ERROR_SUCCESS Then
    G_Reg_SysErrNum = Res
    G_Reg_SysErrText = GetSystemErrorMessageText(ErrorNumber:=Res)
    G_Reg_AppErrNum = C_REG_ERR_UNABLE_TO_DELETE_VALUE
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegCloseKey HKey
    RegistryDeleteValue = False
    Exit Function
End If
    
RegCloseKey HKey
RegistryDeleteValue = True

End Function

Public Function RegistryDeleteKey(BaseKey As Long, KeyName As String) As Boolean
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' RegistryDeleteKey
' This delete the registry key named in KeyName in BaseKey. All subkeys and
' values are deleted. Returns True or False indicating success. Returns
' True if the key does not exist.
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Dim Res As Long
Dim HKey As Long
Dim DataType As REG_DATA_TYPE
Dim SecAttrib As SECURITY_ATTRIBUTES
Dim Disposition As Long

ResetErrorVariables

If IsValidBaseKey(BaseKey:=BaseKey) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryDeleteKey = False
    Exit Function
End If

If IsValidKeyName(KeyName:=KeyName) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryDeleteKey = False
    Exit Function
End If

If RegistryKeyExists(BaseKey:=BaseKey, KeyName:=KeyName, CreateIfNotExists:=False) = False Then
    RegistryDeleteKey = True
    Exit Function
End If

HKey = OpenRegistryKey(BaseKey:=BaseKey, KeyName:=KeyName)
If HKey = 0 Then
    RegistryDeleteKey = False
    Exit Function
End If

Res = RegDeleteKey(HKey:=BaseKey, lpSubKey:=KeyName)
RegCloseKey HKey
If Res &lt;&gt; ERROR_SUCCESS Then
    G_Reg_SysErrNum = Res
    G_Reg_SysErrText = GetSystemErrorMessageText(Res)
    G_Reg_AppErrNum = C_REG_ERR_UNABLE_TO_DELETE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryDeleteKey = False
    Exit Function
End If

RegistryDeleteKey = True
    
End Function

Public Function RegistryUpdateValue(BaseKey As Long, KeyName As String, _
    ValueName As String, NewValue As Variant, Optional CreateIfNotExists As Boolean = True) As Boolean
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' RegistryUpdateValue
' This updates the value of a key. It calls RegistryDeleteValue to delete the
' value and the RegistryCreateValue to re-create the value. Returns True or
' False indicating success.
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Dim Res As Boolean
Dim HKey As Long

ResetErrorVariables

If IsValidBaseKey(BaseKey:=BaseKey) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryUpdateValue = False
    Exit Function
End If

If IsValidKeyName(KeyName:=KeyName) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryUpdateValue = False
    Exit Function
End If

If IsCompatibleValueValue(Var:=NewValue) = False Then
    G_Reg_AppErrNum = C_REG_ERR_INVALID_DATA_TYPE
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryUpdateValue = False
    Exit Function
End If


Res = RegistryKeyExists(BaseKey:=BaseKey, KeyName:=KeyName, CreateIfNotExists:=True)
If Res = False Then
    G_Reg_AppErrNum = C_REG_ERR_KEY_NOT_FOUND
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryUpdateValue = False
    Exit Function
End If

If VarType(NewValue) = vbString Then
    Res = RegistryValueExists(BaseKey:=BaseKey, KeyName:=KeyName, ValueName:=ValueName, _
        CreateIfNotExists:=CreateIfNotExists, CreateType:=REG_DWORD)
Else
    Res = RegistryValueExists(BaseKey:=BaseKey, KeyName:=KeyName, ValueName:=ValueName, _
        CreateIfNotExists:=CreateIfNotExists, CreateType:=REG_SZ)
End If

If Res = False Then
    G_Reg_AppErrNum = C_REG_ERR_VALUE_NOT_FOUND
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    RegistryUpdateValue = False
    Exit Function
End If


Res = RegistryDeleteValue(BaseKey:=BaseKey, KeyName:=KeyName, ValueName:=ValueName)
Res = RegistryCreateValue(BaseKey:=BaseKey, KeyName:=KeyName, ValueName:=ValueName, ValueValue:=NewValue, CreateKeyIfNotExists:=True)

RegistryUpdateValue = Res


End Function


'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Private Functions
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Private Function OpenRegistryKey(BaseKey As Long, KeyName As String) As Long
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' OpenRegistryKey
' This opens the KeyName in BaseKey and returns the key handle
' if successful or 0 if an error occurred.
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Dim Res As Long
Dim HKey As Long

ResetErrorVariables
If IsValidBaseKey(BaseKey) = False Then
    ''''''''''''''''''''''''''''''''''''''
    ' Invalid Base Key. Return 0 and
    ' get out.
    ''''''''''''''''''''''''''''''''''''''
    OpenRegistryKey = 0
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    Exit Function
End If

Res = RegOpenKeyEx(HKey:=BaseKey, lpSubKey:=KeyName, ulOptions:=0&amp;, samDesired:=KEY_ALL_ACCESS, phkResult:=HKey)
If Res &lt;&gt; ERROR_SUCCESS Then
    OpenRegistryKey = 0
    G_Reg_SysErrNum = Res
    G_Reg_SysErrText = GetSystemErrorMessageText(ErrorNumber:=Res)
    G_Reg_AppErrNum = C_REG_ERR_INVALID_BASE_KEY
    G_Reg_AppErrText = GetAppErrText(G_Reg_AppErrNum)
    Exit Function
End If

OpenRegistryKey = HKey


End Function



Private Function TrimToNull(Text As String, _
    Optional Reverse As Boolean = False) As String
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' TrimToNull
' If Reverse is omitted or False, the function returns the
' portion of Text that is to the left of the first vbNullChar
' character. The vbNullChar is not returned. If Reverse is
' True, the function returns the portion to the left of the
' last vbNullChar. The vbNullChar is not returned. In either
' case, if vbNullChar is not found, the entire string Text
' is returned.
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Dim Pos As Long
If Reverse = False Then
    Pos = InStr(1, Text, vbNullChar, vbTextCompare)
Else
    Pos = InStrRev(Text, vbNullChar, -1, vbTextCompare)
End If
If Pos Then
    TrimToNull = Left(Text, Pos - 1)
Else
    TrimToNull = Text
End If

End Function

Private Function TrimToChar(Text As String, Char As String, _
    Optional ByVal Reverse As Boolean = False, _
    Optional ByVal CompareMode As VbCompareMethod) As String
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' If Reverse is False, the function returns the portion of
' Text that is to the left of the first occurrence of Char.
' If Reverse is True, the function returns the portion of
' Text that is to the left of the last occurrence of Char.
' If Char is not found, the entire string Text is returned.
' If CompareMode is vbBinaryCompare, text is compared in
' a CASE-SENSITIVE manner ("A"&lt;&gt;"a"). If CompareMode is any
' other value, text is compared in CASE-INSENSITIVE mode ("A" = "a").
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Dim Pos As Long

If CompareMode &lt;&gt; vbBinaryCompare Then
    CompareMode = vbTextCompare
End If

If Reverse = False Then
    Pos = InStr(1, Text, Char, CompareMode)
Else
    Pos = InStrRev(Text, Char, -1, CompareMode)
End If

If Pos Then
    TrimToChar = Left(Text, Pos - 1)
Else
    TrimToChar = Text
End If


End Function


Private Function IsValidBaseKey(BaseKey As Long) As Boolean
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' IsValidBaseKey
' This returns True of BaseKey is valid base key
' (HKEY_CURRENT_USER etc) or False if BaseKey is not
' a valid base key.
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Select Case BaseKey
    Case HKEY_CURRENT_USER, HKEY_LOCAL_MACHINE, _
            HKEY_CLASSES_ROOT, HKEY_CURRENT_CONFIG, HKEY_DYN_DATA, _
            HKEY_PERFORMANCE_DATA, HKEY_USERS
        IsValidBaseKey = True
    Case Else
        IsValidBaseKey = False
End Select

End Function

Private Sub ResetErrorVariables()
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' ResetErrorVariables
' This resets the global error values to their default
' (no error) values.
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
G_Reg_AppErrNum = C_REG_ERR_NO_ERROR
G_Reg_AppErrText = vbNullString
G_Reg_SysErrNum = C_REG_ERR_NO_ERROR
G_Reg_SysErrText = vbNullString
End Sub

Private Function GetAppErrText(ErrNum As Long) As String
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' GetAppErrText
' This returns the text description of the application error
' number in ErrNum.
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Select Case ErrNum
    Case C_REG_ERR_NO_ERROR
        GetAppErrText = vbNullString
    Case C_REG_ERR_INVALID_BASE_KEY
        GetAppErrText = "Invalid Base Key Value."
    Case C_REG_ERR_INVALID_DATA_TYPE
        GetAppErrText = "Invalid Data Type."
    Case C_REG_ERR_KEY_NOT_FOUND
        GetAppErrText = "Key Not Found."
    Case C_REG_ERR_VALUE_NOT_FOUND
        GetAppErrText = "Value Not Found."
    Case C_REG_ERR_DATA_TYPE_MISMATCH
        GetAppErrText = "Value Data Type Mismatch."
    Case C_REG_ERR_ENTRY_LOCKED
        GetAppErrText = "Registry Entry Locked."
    Case C_REG_ERR_INVALID_KEYNAME
        GetAppErrText = "The Specified Key Is Invalid."
    Case C_REG_ERR_UNABLE_TO_OPEN_KEY
        GetAppErrText = "Unable To Open Key."
    Case C_REG_ERR_UNABLE_TO_READ_KEY
        GetAppErrText = "Unable To Read Key."
    Case C_REG_ERR_UNABLE_TO_CREATE_KEY
        GetAppErrText = "Unable To Create Key."
    Case C_REG_ERR_UBABLE_TO_READ_VALUE
        GetAppErrText = "Unable To Read Value."
    Case C_REG_ERR_UNABLE_TO_UDPATE_VALUE
        GetAppErrText = "Unable To Update Value."
    Case C_REG_ERR_UNABLE_TO_CREATE_VALUE
        GetAppErrText = "Unable To Create Value."
    Case C_REG_ERR_UNABLE_TO_DELETE_KEY
        GetAppErrText = "Unable To Delete Key."
    Case C_REG_ERR_UNABLE_TO_DELETE_VALUE
        GetAppErrText = "Unable To Delete Value."
    
    
    
    
    Case Else
        GetAppErrText = "Undefined Error."
End Select







End Function

Private Function IsStringValidLength(Text As String) As Boolean
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' IsStringValidLength
' This tests whether the length of Text is less than
' REGSTR_MAX_VALUE_LENGTH.
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
IsStringValidLength = (Len(Text) &lt;= REGSTR_MAX_VALUE_LENGTH)

End Function

Private Function IsValidKeyName(KeyName As String) As Boolean
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' IsValidKeyName
' Returns True or False indicating whether KeyName is valid.
' An invalid key is one whose name length is greater than
' REGSTR_MAX_VALUE_LENGTH or is all spaces or is an empty string.
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
IsValidKeyName = (Len(KeyName) &lt;= REGSTR_MAX_VALUE_LENGTH) And (Len(Trim(KeyName)) &gt; 0)
End Function


Private Function IsValidDataType(DataType As REG_DATA_TYPE) As Boolean
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' IsValidDataType
' This returns True or False indicating whether DataType is
' a valid data type (REG_SZ or REG_DWORD).
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    Select Case DataType
        Case REG_SZ, REG_DWORD
            IsValidDataType = True
        Case Else
            IsValidDataType = False
    End Select

End Function

Private Function IsCompatibleValueValue(Var As Variant) As Boolean
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' IsCompatibleValueValue
' This test the VarType of Var to see if it is valid to be used
' as a registry key value. Note that all numeric data types (Singles,
' Doubles, etc) are considered value, even though their values will
' be changed when converted to longs.
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

If VarType(Var) &gt;= vbArray Then
    IsCompatibleValueValue = False
    Exit Function
End If
If IsArray(Var) = True Then
    IsCompatibleValueValue = False
    Exit Function
End If
If IsObject(Var) = True Then
    IsCompatibleValueValue = False
    Exit Function
End If

Select Case VarType(Var)
    Case vbBoolean, vbByte, vbCurrency, vbDate, vbDouble, vbInteger, vbLong, vbSingle, vbString
        IsCompatibleValueValue = True
    Case Else
        IsCompatibleValueValue = False
End Select

End Function</font>
</pre>
	<pre>&nbsp;</pre>
	<p>&nbsp;</p></td>
    <td width="10%" height="22">
      &nbsp;</td>
  </tr>
  <tr>
    <td width="5%">&nbsp;</td>
    <td width="93%">
    &nbsp;</td>
    <td width="1%">
      &nbsp;</td>
  </tr>
  <tr>
    <td width="5%">&nbsp;</td>
    <td width="93%">
    &nbsp;</td>
    <td width="1%">
      &nbsp;</td>
  </tr>
</tbody></table>
<p align="center">&nbsp;

<!-- ================================================ -->
<!-- BEGIN GOOGLE ADSENSE                             -->
<!-- ================================================ -->
<script async="" type="text/javascript" src="./Registry Functions_files/ca-pub-7293698002238456.js"></script><script type="text/javascript"><!--
google_ad_client = "pub-7293698002238456";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_type = "text_image";
google_ad_channel ="";
//--></script>
<script type="text/javascript" src="./Registry Functions_files/show_ads.js">
</script><ins id="aswift_0_expand" style="display:inline-table;border:none;height:90px;margin:0;padding:0;position:relative;visibility:visible;width:728px;background-color:transparent"><ins id="aswift_0_anchor" style="display:block;border:none;height:90px;margin:0;padding:0;position:relative;visibility:visible;width:728px;background-color:transparent"><iframe width="728" height="90" frameborder="0" marginwidth="0" marginheight="0" vspace="0" hspace="0" allowtransparency="true" scrolling="no" allowfullscreen="true" onload="var i=this.id,s=window.google_iframe_oncopy,H=s&amp;&amp;s.handlers,h=H&amp;&amp;H[i],w=this.contentWindow,d;try{d=w.document}catch(e){}if(h&amp;&amp;d&amp;&amp;(!d.body||!d.body.firstChild)){if(h.call){setTimeout(h,0)}else if(h.match){try{h=s.upd(h,i)}catch(e){}w.location.replace(h)}}" id="aswift_0" name="aswift_0" style="left:0;position:absolute;top:0;"></iframe></ins></ins>
<!-- ================================================ -->
<!-- END GOOGLE ADSENSE                             -->
<!-- ================================================ -->




</p><iframe id="google_osd_static_frame_3515659328550" name="google_osd_static_frame" style="display: none; width: 0px; height: 0px;"></iframe><div id="goog_qs_tidbit_box" class="notranslate goog_qs-slideup"><div id="goog_qs-wrapper"><div id="goog_qs-box-toggle" title="Minimizar" style="background-image: url(chrome-extension://okanipcmceoeemlbjnmnbdibhgpbllgc/images/minimize.png);"></div><div id="goog_qs-box-help" title="Abrir a pgina de opes e a ajuda" style="background-image: url(chrome-extension://okanipcmceoeemlbjnmnbdibhgpbllgc/images/help.png);"></div><div id="goog_qs-box-info" style="background-image: url(chrome-extension://okanipcmceoeemlbjnmnbdibhgpbllgc/images/icon16x16.png);"><span class="goog_qs-nowrap">C<img>orrespondncias</span> <span class="goog_qs-nowrap">n<img>a</span> <span class="goog_qs-nowrap">p<img>gina</span> <span class="goog_qs-nowrap">p<img>ara</span> <span class="goog_qs-query"><span class="goog_qs-nowrap">r<img>egedit</span> <span class="goog_qs-nowrap">v<img>ba</span> <span class="goog_qs-nowrap">c<img>ode</span></span></div><div id="goog_qs-box-tidbits"><div class="goog_qs-box-tidbit" title="Clique para ver este texto na pgina"><span class="goog_qs-nowrap">T<img>his</span> <em><span class="goog_qs-nowrap">c<img>ode</span></em> <span class="goog_qs-nowrap">c<img>an</span> <span class="goog_qs-nowrap">b<img>e</span> <span class="goog_qs-nowrap">u<img>sed</span> <span class="goog_qs-nowrap">i<img>n</span> <span class="goog_qs-nowrap">a<img>ny</span> <span class="goog_qs-nowrap">a<img>pplication</span> <span class="goog_qs-nowrap">t<img>hat</span> <span class="goog_qs-nowrap">s<img>upports</span> <em><span class="goog_qs-nowrap">V<img>BA</span></em> <span class="goog_qs-nowrap">o<img>r</span> <span class="goog_qs-nowrap">i<img>n</span> <span class="goog_qs-nowrap">V<img>B6</span><wbr>. <span class="goog_qs-go"></span></div></div><div id="goog_qs-box-bottom"><div class="goog_qs-more-matches" id="goog_qs-box-more-tidbits"><span class="goog_qs-nowrap">M<img>ais</span> <span class="goog_qs-nowrap">c<img>orrespondncias</span> <span class="goog_qs-go"></span></div><div class="goog_qs-more-matches" id="goog_qs-box-fewer-tidbits" style="display:none"><span class="goog_qs-go"></span>  <span class="goog_qs-nowrap">M<img>enos</span> <span class="goog_qs-nowrap">c<img>orrespondncias</span></div><div id="goog_qs-box-feedback-link"><span class="goog_qs-nowrap">F<img>eedback</span></div> <div id="goog_qs-box-new-bug"><span class="goog_qs-nowrap">B<img>ug</span></div></div><div style="clear:both"></div></div></div></body></html>